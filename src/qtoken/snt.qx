// Mondatrabontó alap modul.

header {
    // Az xml tag-ek cserélhetők spec. karakterekre a későbbi feldolgozást
    // megkönnyítendő.
    // Mondat: <s> és </s>
    // Mondat szintű whitespace: <w> és </w>
    // Megj.: A nem mondatszintű whitespace-ek is ugyanilyenek lesznek, de az
    // egy későbbi modul feladata.
    const std::wstring SNT_OPEN = L"<s>";
    const std::wstring SNT_CLOSE = L"</s>";
    const std::wstring WS_OPEN = L"<w>";
    const std::wstring WS_CLOSE = L"</w>";
    /* const std::wstring TEST_OPEN = L"<T>"; */
    /* const std::wstring TEST_CLOSE = L"</T>"; */
}


token {
    // A main számára visszaadott token típusok.
    // MONDAT: mondat-jelölt, a rövidítések kezelése még felülírhatja a
    // tényleges mondathatárokat.
    // WS: (mondat szintű) whitespace
    // ANYCHAR: ismeretlen karkter
    // TODO: ismeretlen karkaterek kezelését kitalálni!
    MONDAT;
    WS;
    ANYCHAR;
}

define {
    // Karakterrosztályok: ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    // EMOTICONS: ~. A quex nem ismeri ezt a unicode blokkot, kézzel kell
    //      megadni
    // BASE_CLASS: alap karakterosztály, minden más ennek részhalmaza kell
    //      legyen
    // NEWLINE: újsor karakterek
    // SPACE: minden wspace a base_class-ból, az újsor karakterek kivételével
    //      Ez a régi: ([ \n\t]) - nincs különbség a ws és nl között.
    // WORDCHAR: szóban szerepelhető karakterek (pont is benne van, l. nytud.hu)
    //      eredetileg html entitásokat is tartalmazott, de ki lettek véve
    //      Ez a régi: ([a-zA-ZáéíóöőúüűÁÉÍÓÖŐÚÜŰ\-.§%°0-9¡£¥¦©ª«¬®¯±³µ¶¹º»¼¾¿ĄŁĽŚŠŞŤŹŽŻąłľśšşťźžżŔÂĂÄĹĆÇČĘËĚÎĎĐŃŇÔŘŮÝŢßŕâăäĺćçčęëěîďđńňôřůýţ])
    //      TODO: html entitások kezelését kitalálni (előfeldolgozás, és
    //          utf8-ra alakítás?)
    // WORDCHAR2: szóban szerepelhető karakterek, pont nélkül
    // LOWER: kisbetűk + néhány szimbólum Csaba kódja alapján
    //      Ez a régi: ([a-zµ¿»¶±¼¾¹³áàăâåäãąæćčçďđðéèêěëęíìîïĺľłńňñóòôöőõøºŕřśšşßťţúùûůüűýźžżþ])
    //      Megj.: union(\P{Lowercase}, \P{Other_Lowercase} is lehet hogy jó
    //          \G{Lowercase_Letter} helyett -- vajon van különbség?
    // CHARINSNT: mondatban szerepelhető, nem szóalkotó és nem is mondathatároló
    //      karakterek (zárójel, idézőjel, szóköz, stb.)
    // BOUNDARY: egyszerű mondathatároló karakterek (pont, felkiáltójel,
    //      kérdőjel)
    //      Megj.: a szón belüli pont máshogy van kezelve.
    // SNTCHAR: nem mondathatároló karakter ami nem is sortörés
    // SNTBEGINCHR: mondatkezdő karakter (pont is lehet, pl. a ".hu vita a
    //           neten.")
    // ENDQUOPAR: záró záró- és idézőjelek
    //      Ez a régi: ("\""|"''"|"'"|")"|"]")
    //      Megj.: mondathatár (BOUNDARY) után is következhetnek (l. SNTEND)
    //      TODO: unicode-osítani
    // INPARENTCHR: BRACKET_PART szabályban zárójelen belül alapból megengedett
    //      karakterek osztálya
    // COMMACHR: COMMA_PART szabályban szerepelhető vessző-szerű karakterek
    
    EMOTICONS   [\U1F600-\U1F915]

    BASE_CLASS  [: union( \P{Block=Basic_Latin},
                          \P{Block=Latin-1_Supplement},
                          \P{Block=Latin_Extended-A},
                          \P{Block=Latin_Extended-B},
                          \P{Block=Latin_Extended-C},
                          \P{Block=Latin_Extended-D},
                          \P{Block=Cyrillic},
                          \P{Block=Cyrillic_Supplement},
                          \P{Block=Greek_and_Coptic},
                          \P{Block=Greek_Extended},
                          \P{Block=Ancient_Greek_Numbers},
                          \P{Block=Combining_Diacritical_Marks},
                          \P{Block=Combining_Diacritical_Marks_Supplement},
                          \P{Block=General_Punctuation},
                          \P{Block=Supplemental_Punctuation},
                          \P{Block=Specials},
                          \P{Block=Mathematical_Operators},
                          \P{Block=Supplemental_Mathematical_Operators},
                          \P{Block=Geometric_Shapes},
                          \P{Block=Arrows},
                          \P{Block=Supplemental_Arrows-A},
                          \P{Block=Supplemental_Arrows-B},
                          \P{Block=Private_Use_Area},
                          \P{Block=Supplementary_Private_Use_Area-A},
                          \P{Block=Supplementary_Private_Use_Area-B},
                          \P{Block=Spacing_Modifier_Letters},
                          \P{Block=Alphabetic_Presentation_Forms},
                          \P{Block=Phonetic_Extensions},
                          \P{Block=Phonetic_Extensions_Supplement},
                          \P{Block=Currency_Symbols},
                          \P{Block=Letterlike_Symbols},
                          \P{Block=Mathematical_Alphanumeric_Symbols},
                          \P{Block=Miscellaneous_Mathematical_Symbols-A},
                          \P{Block=Miscellaneous_Mathematical_Symbols-B},
                          \P{Block=Miscellaneous_Symbols_and_Arrows},
                          \P{Block=Miscellaneous_Technical},
                          \P{Block=Musical_Symbols},
                          \P{Block=Miscellaneous_Symbols},
                          {EMOTICONS}) :]

    NEWLINE     [: intersection( {BASE_CLASS}, [\n\r\f\v] ) :]

    SPACE       [: intersection( {BASE_CLASS}, difference( \P{White_Space}, {NEWLINE}) ) :]

    WORDCHAR    [: intersection( {BASE_CLASS}, union(\P{Alphabetic}, \G{Number}, [.\-_§%°¡£¥¦©ª«¬®¯±µ¶»¿])) :]
 
    WORDCHAR2   [: intersection( {BASE_CLASS}, union(\P{Alphabetic}, \G{Number}, [\-_§%°¡£¥¦©ª«¬®¯±µ¶»¿])) :]

    LOWER       [: intersection( {BASE_CLASS}, \G{Lowercase_Letter} ) :]

    CHARINSNT   [: intersection( {BASE_CLASS}, [\(\)\-[\],; "] ) :]

    BOUNDARY    [: intersection( {BASE_CLASS}, [.?!] ) :]

    SNTCHAR     [: intersection( {BASE_CLASS}, [^.?!\n] ) :]

    SNTBEGINCHR [: intersection( {BASE_CLASS}, [^?!\n ] ) :]

    ENDQUOPAR   [: intersection( {BASE_CLASS}, ["'\)\]] ) :]

    INPARENTCHR [: intersection( {BASE_CLASS}, difference([^.!?\)], {NEWLINE}) ) :]

    COMMACHR    [: intersection( {BASE_CLASS}, [,;:] ) :]

    // Szabályok: ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    // CHARINSNTN: két CHARINSNT (mondaton belüli egyéb karkater), köztük egy
    //             opcionális sortöréssel (a mondatrész-szabályok használják)
    // SNTBEGIN: mondatkazdő karakter, eredetiben utána esetleges sortörés
    //      Eredeti: {SNTBEGINCHR}{NEWLINE}?
    // SIMPLESNT: egyszerű mondat, nem tapadó írásjellel ("Szép az idő !" is jó)
    //       Eredeti: ({SNTCHAR}{NEWLINE}?)*{BOUNDARY}*
    //       Csonkított: ({SNTCHAR}{NEWLINE}?)*
    //       Megj.: mondaton belül egyszeres sortörések megengedettek,
    //          \n\n már paragrafus törésnek számít, a mondat végét
    //          is jelenti. A mondathatároló (.?!) csak opcionális a
    //          mondat végén.
    // SNTEND: mondat végződés
    // INPARENT: BRACKET_PART szabályhoz a zárójelen belüli karakterek között
    //      megengedhető sortörések kezelése

    CHARINSNTN  {CHARINSNT}*{NEWLINE}?{CHARINSNT}*

    SNTBEGIN    {SNTBEGINCHR}

    SIMPLESNT   ({NEWLINE}?{SNTCHAR})*

    SNTEND      ({SPACE}*({ENDQUOPAR}|{BOUNDARY}))*
    // SNTEND      (NEWLINE}?{SPACE}*({ENDQUOPAR}|{BOUNDARY}))*

    INPARENT    ({INPARENTCHR}{NEWLINE}?)*


    // Mondatrészek szabályai: ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    // 0. Egyszerű mondat: megenged egyszeres sortöréseket a mondaton belül, de
    // több sortörés egymás után új paragrafust, és ezzel új mondatot is jelent
	// BE: A kutya ugat. Jó
    // BE: lenne, ha abbahagyná!
	// KI: <s>A kutya ugat.</s> <s>Jó
	// KI: lenne, ha abbahagyná!</s>
	// BE: Ez itt már
	// BE: 
	// BE: külön bekezdés!
	// KI: <s>Ez itt már
	// KI: </s>
	// KI: <s>külön bekezdés!</s>
    // Az egyszerű mondat ilyen szerkezetű: {SNTBEGIN}{SIMPLESNT}{SNTEND}
    // Minden összetettebb esetben az SNTBEGIN után következhet valamilyen
    // részmondat szerűség. Ezek lesznek itt definiálva:
    //      Megj.: a definíciók utáni kommentes definíciók az eredeti, BOUDARY-t
    //      tartalmazó SIMPLESNT hatását állítják helyre

    // 1. Mondathatár túllépés: mondatzáró után kis betű
    // BE: N. kormányzósági székhely.
    // KI: <s>N. kormányzósági székhely.</s>
    // BE: A www.akármi.hu.
    // KI: <s>A www.akármi.hu.</s>
    // BE: - Nézd a! - mondta az egyik.
    // KI: <s>- Nézd a! - mondta az egyik.</s>
    // BE: A 4. javítócsomagot.
    // KI: <s>A 4. javítócsomagot.</s>
    // BE: 3434/1992. évi elszámolás.
    // KI: <s>3434/1992. évi elszámolás.</s>
    // BE: "Jót s jól! Ebben áll a nagy titok" - figyelmezteti Kazinczy költőtársait.
    // KI: <s>"Jót s jól!</s> <s>Ebben áll a nagy titok" - figyelmezteti Kazinczy költőtársait.</s>
    // BE: - Szia Péterkém! Holnap találkozunk - mondta Gizi.
    // KI: <s>- Szia Péterkém!</s> <s>Holnap találkozunk - mondta Gizi.</s>
    // LOWERCASE_PART      ({SIMPLESNT}{BOUNDARY}{SNTEND}{CHARINSNTN}{LOWER})
    LOWERCASE_PART      ({SIMPLESNT}{BOUNDARY}+{SNTEND}*{CHARINSNTN}{LOWER})

    // 2. Mondatzáró után idéző- vagy kötő jel.
    // BE: A "Ne már!"-ral az a baj.
    // KI: <s>A "Ne már!"-ral az a baj.</s>
    // QUOTATIONMARK_PART  ({SIMPLESNT}{BOUNDARY}{SNTEND}"-"{WORDCHAR})
    QUOTATIONMARK_PART  ({SIMPLESNT}{BOUNDARY}+{SNTEND}"-"{WORDCHAR})

    // 3. Pont után közvetlenül szóalkotó karakter a pontot magát leszámítva.
    // BE: A WWW.AKARMI.HU.
    // KI: <s>A WWW.AKARMI.HU.</s>
    // Megj.: url-ben kérdőjel is lehet
    // WORDCHAR_PART       ({SIMPLESNT}[.?]{WORDCHAR2})
    WORDCHAR_PART       ({SIMPLESNT}{BOUNDARY}*[.?]{WORDCHAR2})

    // 4. Mondatzáró után közvetlenül [,;:], utánuk kisbetű
    // BE: Azt mondta, hogy "Na!", "Csináld!" és így tovább.
    // KI: <s>Azt mondta, hogy "Na!", "Csináld!" és így tovább.</s>
    // COMMA_PART          ({SIMPLESNT}{BOUNDARY}{SNTEND}({COMMACHR}|{CHARINSNTN}{LOWER}))
    COMMA_PART          ({SIMPLESNT}{BOUNDARY}+{SNTEND}({COMMACHR}|{CHARINSNTN}{LOWER}))

    // 5. Mondatban lévő zárójeles rész kezelése.
    // BE: A macska (családjában a 25.) Katinak nyávogott.
    // KI: <s>A macska (családjában a 25.) Katinak nyávogott.</s>
    // BRACKET_PART        ({SIMPLESNT}"("{INPARENT}{BOUNDARY}{NEWLINE}?{INPARENT}")")
    BRACKET_PART        ({SIMPLESNT}{BOUNDARY}*"("{INPARENT}{BOUNDARY}{NEWLINE}?{INPARENT}")")
}

mode SNT
{

    // mondatokat leíró teljes, kombinált szabály
    // Megj.: A mondatrészek szabályainak végén nem lehet sortörés, az itt van
    // egységesen kezelve
    {SNTBEGIN}(({LOWERCASE_PART}
               |{QUOTATIONMARK_PART}
               |{WORDCHAR_PART}
               |{COMMA_PART}
               |{BRACKET_PART}
               ){NEWLINE}?)*{SIMPLESNT}{SNTEND} 
    {
        std::wstring LEX(Lexeme);
        LEX = SNT_OPEN + LEX + SNT_CLOSE;
        self_send1(SNT_MONDAT, LEX.c_str());
    }

    /* // unicode teszt-szabály */
    /* {BASE_CLASS} */
    /* { */
    /*     std::wstring LEX(Lexeme); */
    /*     LEX = TEST_OPEN + LEX + TEST_CLOSE; */
    /*     std::wcout << LEX << std::endl; */
    /* } */

    // mondatközi whitespace-eket leíró szabály
    ({SPACE}|{NEWLINE})+   {
        std::wstring LEX(Lexeme);
        LEX = WS_OPEN + LEX + WS_CLOSE;
        self_send1(SNT_WS, LEX.c_str());
        /* std::wcout << LEX << std::endl; */
    }
    /* /1* .           => SNT_ANYCHAR(Lexeme); *1/ */
    on_failure  => SNT_TERMINATION;
    /* . */
    /* { */
    /*     std::wstring LEX(Lexeme); */
    /*     LEX = WS_OPEN + LEX + WS_CLOSE; */
    /*     self_send1(SNT_WS, LEX.c_str()); */
    /* } */
    /* /1* .           => SNT_ANYCHAR(Lexeme); *1/ */
    /* on_failure  => SNT_TERMINATION; */
    /* on_failure */
    /* { */
    /*     std::wcout << "gebasz!: " << Lexeme << std::endl; */
    /* } */
    <<EOF>>     => SNT_TERMINATION;
}

// vim:set syntax=cpp:

