// Mondatrabontast korrigalo es roviditeseket kezelo modul.


token {
    // SNTFRAG: mondattoredek
    // WHITESPACE: megtaggelt whitespace
    // ANYCHAR: barmi
    SNTFRAG;
    WHITESPACE;
    ANYCHAR;
}

define {
    // MONTHS
    // roman
    ROMAN_MONTH         ("V"?"I"{1,3}|"I"?[VX]|"X""I"{1,2})
    // arabic
    ARABIC_MONTH        ("0"?[0-9])|("1"[0-2])
    // abbrev with capital letter
    MONTH_CAP (J[aA][nN]|F[eE][bB]|M[áÁ][rR][cC]|Á[pP][rR]|M[áÁ][jJ]|J[Úú][nNlL]|A[uU][gG]|S[zZ][eE][pP][tT]?|O[kK][tT]|N[oO][vV]|D[eE][cC])
    // abbrev
    MONTH_ABBR ([Jj][Aa][Nn]|[Ff][Ee][Bb][rR]?|[Mm][Áá][rR][cC]?|[Áá][pP][rR]|[Mm][Áá][jJ]|[Jj][Úú][nNlL]|[Aa][uU][gG]|[Ss][zZ][eE][pP][tT]?|[Oo][kK][tT]|[NnDd][oOeE][vVcC])
}


// M O D E S
mode PROGRAM {
    // 1. Mondatkezdo sorszamok.
    // BE: <s>25.</s> <s>Magyarország
    // KI: <s>25. Magyarország
    // BE: <s>25.</s> <s>§
    // KI: <s>25. §
    // Megj.: Eredetileg kulon mode volt ra, de igy egyszerubb.
    {SNT_OPEN_QX}{INT}{DOT_SENTENCE_BOUNDARY}{UPPER} {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme));
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }

    // 2. Zarojelezett datum (mondatveg is lehet utana).
    // BE: <s>A 2002.</s><ws> </ws><s>(IV.</s><ws> </ws><s>25.)</s>
    // KI: <s>A 2002. (IV. 25.)</s>
    // BE: <s>A 2002.</s> <s>(IV. 25.) van.
    // KI: <s>A 2002. (IV. 25.) van.
    // BE: <s>A 2002.</s> <s>(IV.25.) van.
    // KI: <s>A 2002. (IV.25.) van.
    // BE: <s>A 2002.</s> <s>(IV.) van.
    // KI: <s>A 2002. (IV.) van.
    // eredeti: [0-9]+{BOUNDARY}"("([VX]?"I"{1,3}|"I"?[VX])"."({SPACE}[0-9]+".")?")</s>"{SPACE}"<s>"
    // Megj.: Mondatkezdo verzio nelkul mondat elejen nem futna le 1. miatt,
    //      mert 1. mar a mondatnyito tag-tol illeszkedik => kell mondatkezdo
    //      verzio.
    {INT}{DOT_SENTENCE_BOUNDARY}"("{ROMAN_MONTH}"."({SENTENCE_BOUNDARY}?{INT}".")?")"{SENTENCE_BOUNDARY}? {
        std::wstring LEX(self.snt_boundary_correction(Lexeme));
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }
    // Mondatkezdo verzio:
    {SNT_OPEN_QX}{INT}{DOT_SENTENCE_BOUNDARY}"("{ROMAN_MONTH}"."({SENTENCE_BOUNDARY}?{INT}".")?")"{SENTENCE_BOUNDARY}? {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme));
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }

    // 3. Roviditett honap + nap.
    // BE: Szep.</s> <s>11-ről
    // KI: Szep. 11-ről
    // eredeti: ([0-9]+{BOUNDARY})?{MONTH_ABBR}{BOUNDARY}[0123]?[0-9]
    ({INT}{DOT_SENTENCE_BOUNDARY})?{MONTH_ABBR}{DOT_SENTENCE_BOUNDARY}[0123]?[0-9] {
        std::wstring LEX(self.snt_boundary_correction(Lexeme));
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }
    // Mondatkezdo verzio:
    {SNT_OPEN_QX}{INT}{DOT_SENTENCE_BOUNDARY}{MONTH_ABBR}{DOT_SENTENCE_BOUNDARY}[0123]?[0-9] {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme));
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }

    // 4. Ev + nagybetus honap.
    // BE: 2012.</s> <s>MÁJUS
    // KI: 2012. MÁJUS
    // BE: 2012.</s> <s>Júl.
    // KI: 2012. Júl.
    // Megj.: Mondat elejen nem fog lefutni 1. miatt, mert 1. mar a mondatnyito
    //      tag-tol illeszkedik, de 1. megfeleloen mukodik ekkor => nem kell
    //      mondatkezdo verzio.
    // eredeti: [0-9]+{BOUNDARY}{MONTH_CAP}
    {INT}{DOT_SENTENCE_BOUNDARY}{MONTH_CAP} {
        std::wstring LEX(self.snt_boundary_correction(Lexeme));
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }

    // 5. Ugyiratszam + datum.
    // BE: <s>A 25/2002.</s> <s>(IV.</s> <s>25.)</s> <s>
    // KI: <s>A 25/2002. (IV. 25.)
    // BE: <s>A 25/2002.</s> <s>(IV. 25.)</s> <s>
    // KI: <s>A 25/2002. (IV. 25.)
    // BE: <s>A 25/2002.</s> <s>(IV.</s> <s>25.)
    // KI: <s>A 25/2002. (IV. 25.)
    // BE: <s>A 25/2002.</s> <s>(IV.25.)
    // KI: <s>A 25/2002. (IV.25.)
    // BE: <s>A 25/2002.</s> <s>(IV.)
    // KI: <s>A 25/2002. (IV.)
    // Megj.: Nem kell, megoldja mar a 2. is. Amugy kell belole mondatkezdo is.
    // eredeti: {SPACE}[0-9]+"/"[0-9]+{BOUNDARY}"("([VX]?"I"{1,3}|"I"?[VX])".</s>"({SPACE}"<s>"[0-9]+".")?")</s>"{SPACE}"<s>"
    // Az eredetibol es a peldakbol kikovetkeztetheto intencionak megfelelo atirat:
    /* {WSPACE}{INT}"/"{INT}{DOT_SENTENCE_BOUNDARY}"("{ROMAN_MONTH}"."(({SENTENCE_BOUNDARY}|{WS_SEQ})?{INT}".")?")"({SENTENCE_BOUNDARY})? { */
    /*     std::wstring LEX(self.snt_boundary_correction(Lexeme)); */
    /*     self_send1(sntcorr_SNTFRAG, LEX.c_str()); */
    /* } */
    // Mondatkezdo verzio:
    /* {SNT_OPEN_QX}{INT}"/"{INT}{DOT_SENTENCE_BOUNDARY}"("{ROMAN_MONTH}"."(({SENTENCE_BOUNDARY}|{WS_SEQ})?{INT}".")?")"({SENTENCE_BOUNDARY})? { */
    /*     self_send1(sntcorr_SNTFRAG, self.SNT_OPEN_CPP.c_str()); */
    /*     std::wstring LEX(self.snt_boundary_correction(Lexeme)); */
    /*     self_send1(sntcorr_SNTFRAG, LEX.c_str()); */
    /* } */

    // 6. Sorszamot koveto szam, paragrafusjel vagy romai honap.
    // BE: <s>A 25.</s> <s>§ szerint 2002.</s> <s>IV. havában.
    // KI: <s>A 25. § szerint 2002. IV. havában.
    // Megj.: Mondatkezdo verzio nelkul mondat elejen nem futna le 1. miatt,
    //      mert 1. mar a mondatnyito tag-tol illeszkedik. 1. megfeleloen
    //      mukodik ekkor, kiveve az INT esetet => kell mondatkezdo verzio.
    //      (Ill. a '§' sem mukodne, ha nem lenne beledefinialva az UPPER-be.)
    // eredeti: [0-9]+{BOUNDARY}([§0-9]|[VX]?"I"{1,3}"."|"I"?[VX]".")
    {INT}{DOT_SENTENCE_BOUNDARY}({ROMAN_MONTH}|{INT}|"§") {
        std::wstring LEX(self.snt_boundary_correction(Lexeme));
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }
    // Mondatkezdo verzio:
    {SNT_OPEN_QX}{INT}{DOT_SENTENCE_BOUNDARY}({ROMAN_MONTH}|{INT}|"§") {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme));
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }

    // 7. Teljes datum romai vagy arab honappal.
    // BE: <s>A 2001.</s> <s>XII.</s> <s>10-en tortentek megraztak a vilagot.</s>
    // KI: <s>A 2001. XII. 10-en tortentek megraztak a vilagot.</s>
    // Megj.: Mondatkezdo verzio nelkul az 1-es futna le ketszer es kivenne az
    //      elso es harmadik mondathatart, de benthagyna a masodikat => kell a
    //      mondatkezdo verzio.
    // problemas eset: "2001. XII. 11. Bori szuletesnapja, nem Gazsie."
    // eredeti: [0-9]+{BOUNDARY}([VX]?"I"{1,3}|"I"?[VX]){BOUNDARY}[0-9]
    // esetleg: {INT}{DOT_SENTENCE_BOUNDARY}{ROMAN_MONTH}{DOT_SENTENCE_BOUNDARY}{INT}({DOT_SENTENCE_BOUNDARY}{UPPER})?
    {INT}{DOT_SENTENCE_BOUNDARY}({ROMAN_MONTH}|{ARABIC_MONTH}){DOT_SENTENCE_BOUNDARY}{INT} {
        std::wstring LEX(self.snt_boundary_correction(Lexeme));
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }
    // Mondatkezdo verzio:
    // esetleg: {SNT_OPEN_QX}{INT}{DOT_SENTENCE_BOUNDARY}{ROMAN_MONTH}{DOT_SENTENCE_BOUNDARY}{INT}({DOT_SENTENCE_BOUNDARY}{UPPER})?
    {SNT_OPEN_QX}{INT}{DOT_SENTENCE_BOUNDARY}({ROMAN_MONTH}|{ARABIC_MONTH}){DOT_SENTENCE_BOUNDARY}{INT} {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme));
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }

    // 8. .* kozti mondathatar korrigalasa (pl. *.*, huntoken.*, stb) /M.I./
    // BE: A .* van.
    // KI: <s>A .* van</s>
    // problema: ha a romai szamos szabalyban nem kellen hack-elni, akkor itt
    // sem kellene a base_class.
    ({BASE_CLASS}|{SNT_OPEN_QX}){DOT_SENTENCE_BOUNDARY}"*" {
        std::wstring LEX(self.snt_boundary_correction(Lexeme));
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }

    // 9. Romai szamok (VI. Lajos), kiveve CD.
    // BE: XIV.</s> <s>Lajos.
    // KI: XIV. Lajos.
    // BE: Ott a CD.</s> <s>Hallod?
    // KI: Ott a CD.</s> <s>Hallod?
    // eredeti: {NONWORDCHAR}"CD"{BOUNDARY} - ez csak ECHO-zva van!
    // eredeti: {NONWORDCHAR}[IVXLCMD]+{BOUNDARY}
    // Problema: ROMAN_NUMBER illeszkedik empty string-re is. Ilyenkor modositas
    // nelkul tovabb kuldjuk az illeszkedo karaktereket. Emiatt viszont ez a
    // szabaly elobb illeszkedik, mint a .*-ra vonatkozo, ezert azt is hack-elni
    // kell.
    ({NONWORDCHAR}|{SNT_OPEN_QX})"CD"{DOT_SENTENCE_BOUNDARY} {
        self_send1(sntcorr_SNTFRAG, Lexeme);
    }
    {NONWORDCHAR}{ROMAN_NUMBER}{DOT_SENTENCE_BOUNDARY} {
        if(Lexeme[1]==L'.'){
            self_send1(sntcorr_SNTFRAG, Lexeme);
        }
        else {
            std::wstring LEX(self.snt_boundary_correction(Lexeme));
            self_send1(sntcorr_SNTFRAG, LEX.c_str());
        }
    }
    // Mondatkezdo verzio:
    // Itt elvileg nem lehet baj az ures romai szambol, mert az ures mondatot
    // feltetelez, azt meg nem kaphatunk.
    {SNT_OPEN_QX}{ROMAN_NUMBER}{DOT_SENTENCE_BOUNDARY} {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme));
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }

    // 10. Monogramok.
    // BE: B.</s> <s>Jenõ.
    // KI: B. Jenõ.
    // BE: A.</s> <s>E.</s> <s>X.</s> <s>Wilson.
    // KI: A. E. X. Wilson.
    // Megj.: Valamiert nem lehet sem a roviditeses szaballyal egyutt, sem
    //      mellette, kul. az elszallhat seg fault-tal olyan bemenetre, ami
    //      rovidites, de nem illeszkedik a szabalyra (pl. "<s>luther</s>")
    // eredeti: {NONWORDCHAR}({ABBREV}{BOUNDARY}|{UPPER}{BOUNDARY})+
    {NONWORDCHAR}({UPPER}{DOT_SENTENCE_BOUNDARY})+ {
        std::wstring LEX(self.snt_boundary_correction(Lexeme));
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }
    // Mondatkezdo verzio:
    {SNT_OPEN_QX}({UPPER}{DOT_SENTENCE_BOUNDARY})+ {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme));
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }
    // Mondatveg + fajlveg verzio:
    {NONWORDCHAR}({UPPER}{DOT_SENTENCE_BOUNDARY})+{UPPER}"."{SNT_CLOSE_QX} {
        std::wstring LEX(self.snt_boundary_correction(Lexeme)+self.SNT_CLOSE_CPP);
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }
    // Mondatkezdo verzio a mondatveg + fajlveg felebol:
    {SNT_OPEN_QX}({UPPER}{DOT_SENTENCE_BOUNDARY})+{UPPER}"."{SNT_CLOSE_QX} {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme)+self.SNT_CLOSE_CPP);
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }

    // 11. Rovidites es kettospont kozti mondathatar (csaba fele 'huje string').
    // Megj.: Ha a roviditesekkel (12.) fel van cserelve, akkor segfault lehet.
    // Megj.: Elvileg nem fut le, nem kell.
    // eredeti: {ALL_ABBREV}"."")"?"</s><s>:"
    /* {NONWORDCHAR}{ALLABBREV}"."")"?{SENTENCE_BOUNDARY}":" { */
    /*     std::wstring LEX(self.snt_boundary_correction(Lexeme)); */
    /*     self_send1(sntcorr_SNTFRAG, LEX.c_str()); */
    /* } */
    /* // Mondatkezdo verzio: */
    /* {SNT_OPEN_QX}{ALLABBREV}"."")"?{SENTENCE_BOUNDARY}":" { */
    /*     std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme)); */
    /*     self_send1(sntcorr_SNTFRAG, LEX.c_str()); */
    /* } */

    // 12. Roviditesek.
    // BE: Pl.</s> <s>Jani szerint ápr.</s> <s>Kati kedvenc hónapja.
    // KI: Pl. Jani szerint ápr. Kati kedvenc hónapja.
    // eredeti: {NONWORDCHAR}({ABBREV}{BOUNDARY}|{UPPER}{BOUNDARY})+
    {NONWORDCHAR}({ABBREV}{DOT_SENTENCE_BOUNDARY})+ {
        std::wstring LEX(self.snt_boundary_correction(Lexeme));
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }
    // Mondatkezdo verzio:
    {SNT_OPEN_QX}({ABBREV}{DOT_SENTENCE_BOUNDARY})+ {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme));
        self_send1(sntcorr_SNTFRAG, LEX.c_str());
    }

    // Mondatkozi whitespace-ek kiirasa:
    {TAGGED_WS_SEQ}     => sntcorr_WHITESPACE(Lexeme);
    // Ami nem illeszkedik, azt siman tovabbkuldjuk:
    on_failure      => sntcorr_ANYCHAR(Lexeme);
    // Fajl vege:
    <<EOF>>         => sntcorr_TERMINATION;
    on_end_of_stream => sntcorr_TERMINATION;
}

// vim:set syntax=cpp:

