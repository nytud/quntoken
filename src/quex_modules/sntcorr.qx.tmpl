// Mondatrabontast korrigalo es roviditeseket kezelo modul.


token {
    // SNTFRAG: mondattoredek
    // WHITESPACE: megtaggelt whitespace
    // ANYCHAR: barmi
    SNTFRAG;
    WHITESPACE;
    ANYCHAR;
}

define {
    // ws szekvencia benne legfeljebb 1 db sortoressel
    WS_SEQ              (({SPACE}+{NEWLINE}?{SPACE}*)|({NEWLINE}{SPACE}*))
    // megtag-gelt ws_seq
    TAGGED_WS_SEQ       ({WS_OPEN_QX}{WS_SEQ}{WS_CLOSE_QX})
    // "." utani mondatzaro-tag, ws, mondatnyito-tag kombinacio:
    DOT_SENTENCE_BOUNDARY   ("."{SNT_CLOSE_QX}{TAGGED_WS_SEQ}{SNT_OPEN_QX})
    // ugyan ez pont nelkul
    SENTENCE_BOUNDARY       ({SNT_CLOSE_QX}{TAGGED_WS_SEQ}{SNT_OPEN_QX})
    // honap romai szammal
    //eredeti: ([VX]?"I"{1,3}|"I"?[VX]) - ez a XIII-at is megengedi
    ROMAN_MONTH         ("V"?"I"{1,3}|"I"?[VX]|"X""I"{1,2})
    // nagybetuvel kezdodo honapnev-roviditesek:
    MONTH_CAP (J[aA][nN]|F[eE][bB]|M[áÁ][rR][cC]|Á[pP][rR]|M[áÁ][jJ]|J[Úú][nNlL]|A[uU][gG]|S[zZ][eE][pP]|O[kK][tT]|N[oO][vV]|D[eE][cC])
    // honapnev-roviditesek:
    MONTH_ABBR ([Jj][Aa][Nn]|[Ff][Ee][Bb][rR]?|[Mm][Áá][rR][cC]?|[Áá][pP][rR]|[Mm][Áá][jJ]|[Jj][Úú][nNlL]|[Aa][uU][gG]|[Ss][zZ][eE][pP][tT]?|[Oo][kK][tT]|[NnDd][oOeE][vVcC])
    // roviditesek (kulso fajlbol generalodik):
    ABBREV  (${ABBREV})
    // ABBREV kiegeszitese 'etc'-vel es 'stb'-vel. Ezek az alap roviditesekbol
    // kimaradnak, hogy mert altalaban mondat vegen vannak, azaz nem kell
    // kiszedni utanuk a mondathatart. A 10-es szabaly rovidites es ':' kozotti
    // mondathatarokat tavolit el, ez esetben a 'etc' es a 'stb' is szamit.
    /* ALLABBREV ({ABBREV}|"etc"|"Etc"|"stb"|"Stb") */
    // Megtaggelt ws szekvencia. Nem keverendo a fenti tagged_ws_seq-kel: az a
    // mondatokon beluli szabalyokhoz kell es csak egy sortores lehet benne,
    // ebben meg barmennyi lehet, egymas utan is, mert mondatok kozotti
    // ws-ekre akar illeszkedni.
    // TODO: egyertelmu megnevezes, ws_seq-kel ne lehessen osszekeverni!
    TAGGED_WS ({WS_OPEN_QX}{NOT_WS_CLOSE_QX}+{WS_CLOSE_QX})

}


// M O D E S
mode PROGRAM {
    // 1. Mondatkezdo sorszamok.
    // BE: <s>25.</s> <s>Magyarország
    // KI: <s>25. Magyarország
    // BE: <s>25.</s> <s>§
    // KI: <s>25. §
    // Megj.: Eredetileg kulon mode volt ra, de igy egyszerubb.
    {SNT_OPEN_QX}{INT}{DOT_SENTENCE_BOUNDARY}{UPPER} {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme));
        self_send1(SNTCORR_SNTFRAG, LEX.c_str());
    }

    // 2. Zarojelezett datum (mondatveg is lehet utana).
    // BE: <s>A 2002.</s><w> </w><s>(IV.</s><w> </w><s>25.)</s>
    // KI: <s>A 2002. (IV. 25.)</s>
    // BE: <s>A 2002.</s> <s>(IV. 25.) van.
    // KI: <s>A 2002. (IV. 25.) van.
    // BE: <s>A 2002.</s> <s>(IV.25.) van.
    // KI: <s>A 2002. (IV.25.) van.
    // BE: <s>A 2002.</s> <s>(IV.) van.
    // KI: <s>A 2002. (IV.) van.
    // eredeti: [0-9]+{BOUNDARY}"("([VX]?"I"{1,3}|"I"?[VX])"."({SPACE}[0-9]+".")?")</s>"{SPACE}"<s>"
    // Megj.: Mondatkezdo verzio nelkul mondat elejen nem futna le 1. miatt,
    //      mert 1. mar a mondatnyito tag-tol illeszkedik => kell mondatkezdo
    //      verzio.
    {INT}{DOT_SENTENCE_BOUNDARY}"("{ROMAN_MONTH}"."({SENTENCE_BOUNDARY}?{INT}".")?")"{SENTENCE_BOUNDARY}? {
        std::wstring LEX(self.snt_boundary_correction(Lexeme));
        self_send1(SNTCORR_SNTFRAG, LEX.c_str());
    }
    // Mondatkezdo verzio:
    {SNT_OPEN_QX}{INT}{DOT_SENTENCE_BOUNDARY}"("{ROMAN_MONTH}"."({SENTENCE_BOUNDARY}?{INT}".")?")"{SENTENCE_BOUNDARY}? {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme));
        self_send1(SNTCORR_SNTFRAG, LEX.c_str());
    }

    // 3. Roviditett honap + nap.
    // BE: Szep.</s> <s>11-ről
    // KI: Szep. 11-ről
    // eredeti: ([0-9]+{BOUNDARY})?{MONTH_ABBR}{BOUNDARY}[0123]?[0-9]
    ({INT}{DOT_SENTENCE_BOUNDARY})?{MONTH_ABBR}{DOT_SENTENCE_BOUNDARY}[0123]?[0-9] {
        std::wstring LEX(self.snt_boundary_correction(Lexeme));
        self_send1(SNTCORR_SNTFRAG, LEX.c_str());
    }
    // Mondatkezdo verzio:
    {SNT_OPEN_QX}{INT}{DOT_SENTENCE_BOUNDARY}{MONTH_ABBR}{DOT_SENTENCE_BOUNDARY}[0123]?[0-9] {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme));
        self_send1(SNTCORR_SNTFRAG, LEX.c_str());
    }

    // 4. Ev + nagybetus honap.
    // BE: 2012.</s> <s>MÁJUS
    // KI: 2012. MÁJUS
    // BE: 2012.</s> <s>Júl.
    // KI: 2012. Júl.
    // Megj.: Mondat elejen nem fog lefutni 1. miatt, mert 1. mar a mondatnyito
    //      tag-tol illeszkedik, de 1. megfeleloen mukodik ekkor => nem kell
    //      mondatkezdo verzio.
    // eredeti: [0-9]+{BOUNDARY}{MONTH_CAP}
    {INT}{DOT_SENTENCE_BOUNDARY}{MONTH_CAP} {
        std::wstring LEX(self.snt_boundary_correction(Lexeme));
        self_send1(SNTCORR_SNTFRAG, LEX.c_str());
    }

    // 5. Ugyiratszam + datum.
    // BE: <s>A 25/2002.</s> <s>(IV.</s> <s>25.)</s> <s>
    // KI: <s>A 25/2002. (IV. 25.)
    // BE: <s>A 25/2002.</s> <s>(IV. 25.)</s> <s>
    // KI: <s>A 25/2002. (IV. 25.)
    // BE: <s>A 25/2002.</s> <s>(IV.</s> <s>25.)
    // KI: <s>A 25/2002. (IV. 25.)
    // BE: <s>A 25/2002.</s> <s>(IV.25.)
    // KI: <s>A 25/2002. (IV.25.)
    // BE: <s>A 25/2002.</s> <s>(IV.)
    // KI: <s>A 25/2002. (IV.)
    // Megj.: Nem kell, megoldja mar a 2. is. Amugy kell belole mondatkezdo is.
    // eredeti: {SPACE}[0-9]+"/"[0-9]+{BOUNDARY}"("([VX]?"I"{1,3}|"I"?[VX])".</s>"({SPACE}"<s>"[0-9]+".")?")</s>"{SPACE}"<s>"
    // Az eredetibol es a peldakbol kikovetkeztetheto intencionak megfelelo atirat:
    /* {WSPACE}{INT}"/"{INT}{DOT_SENTENCE_BOUNDARY}"("{ROMAN_MONTH}"."(({SENTENCE_BOUNDARY}|{WS_SEQ})?{INT}".")?")"({SENTENCE_BOUNDARY})? { */
    /*     std::wstring LEX(self.snt_boundary_correction(Lexeme)); */
    /*     self_send1(SNTCORR_SNTFRAG, LEX.c_str()); */
    /* } */
    // Mondatkezdo verzio:
    /* {SNT_OPEN_QX}{INT}"/"{INT}{DOT_SENTENCE_BOUNDARY}"("{ROMAN_MONTH}"."(({SENTENCE_BOUNDARY}|{WS_SEQ})?{INT}".")?")"({SENTENCE_BOUNDARY})? { */
    /*     self_send1(SNTCORR_SNTFRAG, self.SNT_OPEN_CPP.c_str()); */
    /*     std::wstring LEX(self.snt_boundary_correction(Lexeme)); */
    /*     self_send1(SNTCORR_SNTFRAG, LEX.c_str()); */
    /* } */

    // 6. Sorszamot koveto szam, paragrafusjel vagy romai honap.
    // BE: <s>A 25.</s> <s>§ szerint 2002.</s> <s>IV. havában.
    // KI: <s>A 25. § szerint 2002. IV. havában.
    // Megj.: Mondatkezdo verzio nelkul mondat elejen nem futna le 1. miatt,
    //      mert 1. mar a mondatnyito tag-tol illeszkedik. 1. megfeleloen
    //      mukodik ekkor, kiveve az INT esetet => kell mondatkezdo verzio.
    //      (Ill. a '§' sem mukodne, ha nem lenne beledefinialva az UPPER-be.)
    // eredeti: [0-9]+{BOUNDARY}([§0-9]|[VX]?"I"{1,3}"."|"I"?[VX]".")
    {INT}{DOT_SENTENCE_BOUNDARY}({ROMAN_MONTH}|{INT}|"§") {
        std::wstring LEX(self.snt_boundary_correction(Lexeme));
        self_send1(SNTCORR_SNTFRAG, LEX.c_str());
    }
    // Mondatkezdo verzio:
    {SNT_OPEN_QX}{INT}{DOT_SENTENCE_BOUNDARY}({ROMAN_MONTH}|{INT}|"§") {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme));
        self_send1(SNTCORR_SNTFRAG, LEX.c_str());
    }

    // 7. Teljes datum romai honappal.
    // BE: <s>A 2001.</s> <s>XII.</s> <s>10-en tortentek megraztak a vilagot.</s>
    // KI: <s>A 2001. XII. 10-en tortentek megraztak a vilagot.</s>
    // Megj.: Mondatkezdo verzio nelkul az 1-es futna le ketszer es kivenne az
    //      elso es harmadik mondathatart, de benthagyna a masodikat => kell a
    //      mondatkezdo verzio.
    // problemas eset: "2001. XII. 11. Bori szuletesnapja, nem Gazsie."
    // eredeti: [0-9]+{BOUNDARY}([VX]?"I"{1,3}|"I"?[VX]){BOUNDARY}[0-9]
    // esetleg: {INT}{DOT_SENTENCE_BOUNDARY}{ROMAN_MONTH}{DOT_SENTENCE_BOUNDARY}{INT}({DOT_SENTENCE_BOUNDARY}{UPPER})?
    {INT}{DOT_SENTENCE_BOUNDARY}{ROMAN_MONTH}{DOT_SENTENCE_BOUNDARY}{INT} {
        std::wstring LEX(self.snt_boundary_correction(Lexeme));
        self_send1(SNTCORR_SNTFRAG, LEX.c_str());
    }
    // Mondatkezdo verzio:
    // esetleg: {SNT_OPEN_QX}{INT}{DOT_SENTENCE_BOUNDARY}{ROMAN_MONTH}{DOT_SENTENCE_BOUNDARY}{INT}({DOT_SENTENCE_BOUNDARY}{UPPER})?
    {SNT_OPEN_QX}{INT}{DOT_SENTENCE_BOUNDARY}{ROMAN_MONTH}{DOT_SENTENCE_BOUNDARY}{INT} {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme));
        self_send1(SNTCORR_SNTFRAG, LEX.c_str());
    }

    // 8. Romai szamok (VI. Lajos), kiveve CD.
    // BE: XIV.</s> <s>Lajos.
    // KI: XIV. Lajos.
    // BE: Ott a CD.</s> <s>Hallod?
    // KI: Ott a CD.</s> <s>Hallod?
    // eredeti: {NONWORDCHAR}"CD"{BOUNDARY} - ez csak ECHO-zva van!
    // eredeti: {NONWORDCHAR}[IVXLCMD]+{BOUNDARY}
    ({NONWORDCHAR}|{SNT_OPEN_QX})"CD"{DOT_SENTENCE_BOUNDARY} {
        self_send1(SNTCORR_SNTFRAG, Lexeme);
    }
    {NONWORDCHAR}{ROMAN_NUM}{DOT_SENTENCE_BOUNDARY} {
        std::wstring LEX(self.snt_boundary_correction(Lexeme));
        self_send1(SNTCORR_SNTFRAG, LEX.c_str());
    }
    // Mondatkezdo verzio:
    {SNT_OPEN_QX}{ROMAN_NUM}{DOT_SENTENCE_BOUNDARY} {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme));
        self_send1(SNTCORR_SNTFRAG, LEX.c_str());
    }

    // 9. Monogramok.
    // BE: B.</s> <s>Jenõ.
    // KI: B. Jenõ.
    // BE: A.</s> <s>E.</s> <s>X.</s> <s>Wilson.
    // KI: A. E. X. Wilson.
    // Megj.: Valamiert nem lehet sem a roviditeses szaballyal egyutt, sem
    //      mellette, kul. az elszallhat seg fault-tal olyan bemenetre, ami
    //      rovidites, de nem illeszkedik a szabalyra (pl. "<s>luther</s>")
    // eredeti: {NONWORDCHAR}({ABBREV}{BOUNDARY}|{UPPER}{BOUNDARY})+
    {NONWORDCHAR}({UPPER}{DOT_SENTENCE_BOUNDARY})+ {
        std::wstring LEX(self.snt_boundary_correction(Lexeme));
        self_send1(SNTCORR_SNTFRAG, LEX.c_str());
    }
    // Mondatkezdo verzio:
    {SNT_OPEN_QX}({UPPER}{DOT_SENTENCE_BOUNDARY})+ {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme));
        self_send1(SNTCORR_SNTFRAG, LEX.c_str());
    }

    // 10. Rovidites es kettospont kozti mondathatar (csaba fele 'huje string').
    // Megj.: Ha a roviditesekkel (10.) fel van cserelve, akkor segfault lehet.
    // Megj.: Elvileg nem fut le, nem kell.
    // eredeti: {ALL_ABBREV}"."")"?"</s><s>:"
    /* {NONWORDCHAR}{ALLABBREV}"."")"?{SENTENCE_BOUNDARY}":" { */
    /*     std::wstring LEX(self.snt_boundary_correction(Lexeme)); */
    /*     self_send1(SNTCORR_SNTFRAG, LEX.c_str()); */
    /* } */
    /* // Mondatkezdo verzio: */
    /* {SNT_OPEN_QX}{ALLABBREV}"."")"?{SENTENCE_BOUNDARY}":" { */
    /*     std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme)); */
    /*     self_send1(SNTCORR_SNTFRAG, LEX.c_str()); */
    /* } */

    // 11. Roviditesek.
    // BE: Pl.</s> <s>Jani szerint ápr.</s> <s>Kati kedvenc hónapja.
    // KI: Pl. Jani szerint ápr. Kati kedvenc hónapja.
    // eredeti: {NONWORDCHAR}({ABBREV}{BOUNDARY}|{UPPER}{BOUNDARY})+
    {NONWORDCHAR}({ABBREV}{DOT_SENTENCE_BOUNDARY})+ {
        std::wstring LEX(self.snt_boundary_correction(Lexeme));
        self_send1(SNTCORR_SNTFRAG, LEX.c_str());
    }
    // Mondatkezdo verzio:
    {SNT_OPEN_QX}({ABBREV}{DOT_SENTENCE_BOUNDARY})+ {
        std::wstring LEX(self.SNT_OPEN_CPP+self.snt_boundary_correction(Lexeme));
        self_send1(SNTCORR_SNTFRAG, LEX.c_str());
    }


    ///////////////////////////////////////////////////////////////////////////
    // TODO: Az alabbiakat a posttokenizalashoz felhasznalni! Ha megvan,
    //      torolhetok.
    // -----------------------------------------------
    // tokenizálás után mondatvégirövidítés-javítás
    // nem mukodnek az uj kimenetformatum miatt, at kell oket
    // alakitani, ha kellenek egyaltalan
    // -----------------------------------------------
    //
    // 13. pont visszaragasztasa a roviditesre, romai szamra, monogramra,
    //      kiveve a CD-t
    // Megj.: tokenizalas utan kell jojjon
    // eredeti:
    // "<w>CD\n</w>\n<c>.</c>" { ECHO; }
    // "<w>"({ABBREV}|[A-Z]|[IVXLCMD]+)"\n</w>\n<c>.</c>" {
    // 	strcpy(yytext + (yyleng - 14), ".\n</w>");
    // 	printf("%s", yytext);
    // }
    //
    // 14. ez teszi vissza a pontot a megadott elemek vegere (O.Cs. ???)
    // Megj.: tokenizalas utan kell jojjon
    // eredeti:
    // ^{ALL_ABBREV}"\tTOK\n.\tWPUNCT\n" {
    //     char c[10];
    //     int l  = yyleng - 14;
    //     strncpy(c, yytext, l);
    //     c[l] = '.';
    //     c[l + 1] = '\0';
    //     printf("%s\t%s[:Oyo-sn:]", c, c);
    //     yyless(yyleng - 1);
    // }
    //
    // 15. ??? (O.Cs.)
    // Megj.: tokenizalas utan kell jojjon
    // eredeti:
    // ^([A-Z]|[IVXLCMD]+)"\tTOK\n.\tWPUNCT" {
    //     strcpy(yytext + (yyleng - 13), ".\tTOK");
    //     printf("%s", yytext);
    // }
    //
    // 16. ???
    // Megj.: tokenizalas utan kell jojjon
    // eredeti:
    // "WPUNCT"[\n]+"</s>" {
    //     printf("SPUNCT\n</s>");
    // }
    //
    // 17. hun_sentclean kiegészítése
    // Megj.: tokenizalas utan kell jojjon
    // eredeti:
    // [\n]+"</s>" {
    //     printf("\n</s>");
    // }
    ///////////////////////////////////////////////////////////////////////////


    // mondatkozi whitespace-ek kiirasa:
    {TAGGED_WS}     => SNTCORR_WHITESPACE(Lexeme);
    // Megj.: a korrigalasra nem szorulo mondatok es mondatreszletek kiirasa
    // karakterenkent tortenik, mert nem megoldott azon
    // mondatok elejere valo illesztes, melyek (kesobb) tartalmaznek
    // korrigalando reszletet, hiszen a teljes mondatra illeszkedo szabaly fog
    // mindig lefutni a hosszabb illeszkedes miatt. Ennek az illesztesnek
    // raadasul lustanak kellene lennie, ami meg kevesbe megoldott.
    // barmi mas kiirasa:
    .               => SNTCORR_ANYCHAR(Lexeme);
    {NEWLINE}       => SNTCORR_ANYCHAR(Lexeme);
    on_after_match  {
        std::cerr << QUEX_CONVERTER_STRING(unicode, char)(Lexeme);
    }
    <<EOF>>         => SNTCORR_TERMINATION;
    on_failure      => SNTCORR_TERMINATION;
}

// vim:set syntax=cpp:

